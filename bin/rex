#!perl

#
# (c) Jan Gehring <jan.gehring@gmail.com>
# 
# vim: set ts=3 sw=3 tw=0:
# vim: set expandtab:


use strict;
use warnings;

use FindBin;

use lib "$FindBin::Bin/../lib";

use Rex;
use Rex::Config;
use Rex::Group;
use Rex::Task;
use Rex::Commands;
use Rex::Commands::Run;
use Rex::Commands::Upload;

use Getopt::Std;

eval {
   require Scope::With;
   Scope::With->import();
};

if($@) {
   print STDERR "[warn] no Scope::With found\n";
}

my %opts;
getopts('Thvf:', \%opts);

if($opts{'h'}) {
   print "(R)?ex - (Remote)? Execution\n";
   printf "  %-15s %s\n", "-T", "List all known tasks.";
   printf "  %-15s %s\n", "-f", "Use this file instead of Rexfile";
   printf "  %-15s %s\n", "-h", "Display this help";
   printf "  %-15s %s\n", "-v", "Display (R)?ex Version";
   exit 0;
} elsif($opts{'v'}) {
   print "(R)?ex " . $Rex::VERSION . "\n";
   exit 0;
}

my $rexfile = "Rexfile";
if($opts{'f'}) {
   $rexfile = $opts{'f'};
}

if(-f $rexfile) {
   if(-f "$rexfile.lock") {
      print STDERR "Rexfile in use.\n";
      exit 1;
   }
   open(F, ">$rexfile.lock") or die($!);
   close(F);
   eval {
      do($rexfile);
   };

   if($@) { print $@ . "\n"; exit 1; }
} else {
   print STDERR "No Rexfile found.\n";
   exit 1;
}

if($opts{'T'}) {
   print "Tasks\n";
   for my $task (Rex::Task->get_tasks) {
      printf "  %-30s %s\n", $task, Rex::Task->get_desc($task);
   }
}

if(defined $ARGV[0]) {
   if(Rex::Task->is_task($ARGV[0])) {
      Rex::Task->run($ARGV[0]);
   }
}

# lock loeschen
unlink("$rexfile.lock");


